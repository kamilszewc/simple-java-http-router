plugins {
    id 'java-library'
    id 'java'
    id 'maven-publish' // To publish into github
    id 'signing' // Required by maven central
    id 'org.sonatype.gradle.plugins.scan' version '2.8.2'
}

group = 'io.github.kamilszewc'
version = '0.1'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                if (project.properties.containsKey('mavenCentralUser')) {
                    username = "$mavenCentralUser"
                }
                if (project.properties.containsKey('mavenCentralPassword')) {
                    password = "$mavenCentralPassword"
                }
            }
        }
    }
    publications {

        mavenJava(MavenPublication) {
            artifactId = 'simple-router'
            from components.java
            pom {
                name = 'Simple rest-api router'
                description = 'Simple rest-api router'
                url = 'https://github.com/kamilszewc/simple-router'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kszewc'
                        name = "Kamil Szewc"
                        email = 'kamil.szewc@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/kamilszewc/simple-router.git'
                    developerConnection = 'scm:git:https://github.com/kamilszewc/simple-router.git'
                    url = 'https://github.com/kamilszewc/simple-router.git'
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
    sign configurations.archives
}

ossIndexAudit {
    allConfigurations = false
    useCache = false
    outputFormat = 'DEFAULT'
}